/*
 * MIT License
 *
 * Copyright (c) 2020 Nima Karimipour
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */

plugins {
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
    id 'application'
}

application {
    mainClass = 'edu.ucr.cs.riple.core.Main'
}

dependencies {

    implementation project(':injector')
    implementation project(':type-annotator-scanner')
    implementation deps.build.guava
    implementation deps.build.json
    implementation deps.build.progressbar
    implementation deps.build.javaparser
    implementation deps.build.commonscli
    testImplementation deps.build.commonsio
}

tasks.test.dependsOn(':type-annotator-scanner:publishToMavenLocal')

// Set up environment variables for test configuration tu run with the latest development version.
tasks.test.doFirst {
    environment "NULLAWAY_TEST_VERSION", "0.9.10-SNAPSHOT"
    environment "TYPE_ANNOTATOR_SCANNER_VERSION", project.version
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
    repositories {
        mavenLocal()
    }
}

jar {
    // add this classifier to prevent overwriting jar and shadowJar outputs to preserve incremental build of gradle.
    archiveClassifier = "nonshadow"
}

// To remove the "-all" postfix from the produced jar file.
shadowJar {
    archiveClassifier = null
}



// TODO: Remove below once NullAway 0.9.10 is released
tasks.test {
    filter {
        //exclude all tests below as they are only compatible with NullAway 0.9.10 (The upcoming version).
        excludeTestsMatching "edu.ucr.cs.riple.core.CoreTest"
        excludeTestsMatching "edu.ucr.cs.riple.core.DeepTest"
        excludeTestsMatching "edu.ucr.cs.riple.core.InheritanceTest"
        excludeTestsMatching "edu.ucr.cs.riple.core.DownstreamAnalysisTest"
        excludeTestsMatching "edu.ucr.cs.riple.core.ArgumentNullableFlowTest"
    }
}
